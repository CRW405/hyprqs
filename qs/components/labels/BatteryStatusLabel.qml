import QtQuick
import "../common"

SlotText {
    id: root
    property string status: "Unknown"
    property int timeToFullSec: 0
    property bool showEstimate: false
    property bool hasBattery: true

    function formatTime(seconds) {
        if (!seconds || seconds <= 0) return ""
        var totalMinutes = Math.round(seconds / 60)
        var hours = Math.floor(totalMinutes / 60)
        var minutes = totalMinutes % 60
        if (hours > 0) return hours + "h " + minutes + "m"
        return minutes + "m"
    }

    function baseLabel() {
        if (status === "Charging") return "Charging"
        if (status === "Discharging") return "Discharging"
        if (status === "Full") return "Full"
        return "Battery"
    }

    widthSamples: ["Charging 23h 59m", "Discharging", "Full", "Battery"]
    visible: root.hasBattery
    content: labelText

    StyledText {
        id: labelText
        text: {
            if (!root.hasBattery) return ""
            var label = root.baseLabel()
            if (root.showEstimate && root.status === "Charging") {
                var eta = root.formatTime(root.timeToFullSec)
                if (eta) label += " " + eta
            }
            return label
        }
    }

    MouseArea {
        anchors.fill: parent
        onClicked: root.showEstimate = !root.showEstimate
    }
}
