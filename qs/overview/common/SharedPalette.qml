pragma Singleton
import QtQuick
import Quickshell
import Quickshell.Io

// Own copy of qs/theme/Palette.qml's style.json reader: quickshell runs qs/overview as a
// separate process rooted at qs/overview, so a singleton living outside that root (qs/theme)
// can't be imported here - "Module path is outside of the config folder" breaks resolution.
// Defaults below only apply until style.json loads.
QtObject {
	id: root

	property QtObject color: QtObject {
		property color primary: "#5c9dff"
		property color secondary: "#b39dff"
		property color danger: "#ff6b6b"
		property color warning: "#ffcc00"
		property color ok: "#4caf50"
		property color active: "#45454e"
		property color inactive: "#6a6a72"
		property color background: "#1c1c1e"
		property color background_light: "#3a3a40"
		property color background_dark: "#2a2a2e"
		property color border: "#6e6e78"
		property color border_light: "#5a6b9c"
		property color border_dark: "#241b40"
		property color text: "#d4d4d8"
		property color text_secondary: "#6a6a72"
		property color text_light: "#ffffff"
		property color text_dark: "#1c1c1e"
	}

	property QtObject font: QtObject {
		property string family: "Monospace"
		property int size: 12
	}

	property int radius: 10

	property QtObject spacing: QtObject {
		property int gap: 5
		property int padding: 10
	}

	property FileView _file: FileView {
		// Quickshell.shellDir is this process's -p root (qs/overview), two levels above style/style.json.
		path: Quickshell.shellDir + "/../../style/style.json"
		watchChanges: true
		onFileChanged: reload()
		onLoaded: {
			try {
				root._apply(JSON.parse(root._stripComments(text())))
			} catch (e) {
				console.warn("SharedPalette: failed to parse style/style.json:", e)
			}
		}
		onLoadFailed: error => console.warn("SharedPalette: failed to load style/style.json:", error)
	}

	// strips // and /* */ comments outside of string literals so style.json can support JSONC
	function _stripComments(s) {
		let out = ""
		let inString = false
		let inLine = false
		let inBlock = false
		for (let i = 0; i < s.length; i++) {
			const c = s[i]
			const next = s[i + 1]
			if (inLine) {
				if (c === "\n") { inLine = false; out += c }
				continue
			}
			if (inBlock) {
				if (c === "*" && next === "/") { inBlock = false; i++ }
				continue
			}
			if (inString) {
				out += c
				if (c === "\\") { out += next; i++ }
				else if (c === "\"") inString = false
				continue
			}
			if (c === "\"") { inString = true; out += c }
			else if (c === "/" && next === "/") { inLine = true; i++ }
			else if (c === "/" && next === "*") { inBlock = true; i++ }
			else out += c
		}
		return out
	}

	function _apply(data) {
		if (data.color) {
			for (const key in data.color) {
				if (root.color.hasOwnProperty(key)) {
					root.color[key] = "#" + data.color[key]
				}
			}
		}
		if (data.font) {
			if (data.font.family)
				root.font.family = data.font.family
			if (data.font.size)
				root.font.size = data.font.size
		}
		if (data.radius !== undefined)
			root.radius = data.radius
		if (data.spacing) {
			if (data.spacing.gap !== undefined)
				root.spacing.gap = data.spacing.gap
			if (data.spacing.padding !== undefined)
				root.spacing.padding = data.spacing.padding
		}
	}
}
